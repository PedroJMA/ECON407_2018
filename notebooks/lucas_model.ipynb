{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Notebook created: 2018-03-29 04:30:33  \n",
    "Generated from: _build_jl/jl/lucas_model.rst  "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#=\n",
    "\n",
    "@authors : Spencer Lyon <spencer.lyon@nyu.edu>, John Stachurski\n",
    "\n",
    "=#\n",
    "\n",
    "using QuantEcon\n",
    "using Distributions\n",
    "\n",
    "\"\"\"\n",
    "The Lucas asset pricing model --- parameters and grid data\n",
    "\"\"\"\n",
    "struct LucasTree{TF<:AbstractFloat}\n",
    "    γ::TF                 # coefficient of risk aversion\n",
    "    β::TF                 # Discount factor in (0, 1)\n",
    "    α::TF                 # Correlation coefficient in the shock process\n",
    "    σ::TF                 # Volatility of shock process\n",
    "    ϕ::Distribution       # Distribution for shock process\n",
    "    grid::Vector{TF}      # Grid of points on which to evaluate prices\n",
    "    shocks::Vector{TF}    # Draws of the shock\n",
    "    h::Vector{TF}         # The h function represented as a vector\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "Constructor for the Lucas asset pricing model\n",
    "\"\"\"\n",
    "function LucasTree(;γ::AbstractFloat=2.0,\n",
    "                β::AbstractFloat=0.95,\n",
    "                α::AbstractFloat=0.9,\n",
    "                σ::AbstractFloat=0.1,\n",
    "                grid_size::Integer=100)\n",
    "\n",
    "    ϕ = LogNormal(0.0, σ)\n",
    "    shocks = rand(ϕ, 500)\n",
    "\n",
    "    # == build a grid with mass around stationary distribution == #\n",
    "    ssd = σ / sqrt(1 - α^2)\n",
    "    grid_min, grid_max = exp(-4 * ssd), exp(4 * ssd)\n",
    "    grid = collect(linspace(grid_min, grid_max, grid_size))\n",
    "\n",
    "    # == set h(y) = β * int u'(G(y,z)) G(y,z) ϕ(dz) == #\n",
    "    h = similar(grid)\n",
    "    for (i, y) in enumerate(grid)\n",
    "        h[i] = β * mean((y^α .* shocks).^(1 - γ))\n",
    "    end\n",
    "\n",
    "    return LucasTree(γ,\n",
    "                     β,\n",
    "                     α,\n",
    "                     σ,\n",
    "                     ϕ,\n",
    "                     grid,\n",
    "                     shocks,\n",
    "                     h)\n",
    "end\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "The approximate Lucas operator, which computes and returns updated function\n",
    "Tf on the grid points.\n",
    "\"\"\"\n",
    "function lucas_operator(lt::LucasTree, f::Vector)\n",
    "\n",
    "    # == unpack names == #\n",
    "    grid, α, β, h = lt.grid, lt.α, lt.β, lt.h\n",
    "    z = lt.shocks\n",
    "\n",
    "    Af = LinInterp(grid, f)\n",
    "\n",
    "    Tf = [h[i] + β * mean(Af.(grid[i]^α.*z)) for i in 1:length(grid)]\n",
    "    return Tf\n",
    "end\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "Compute the equilibrium price function associated with Lucas tree `lt`\n",
    "\"\"\"\n",
    "function solve_lucas_model(lt::LucasTree;\n",
    "                           tol::AbstractFloat=1e-6, \n",
    "                           max_iter::Integer=500)\n",
    "    \n",
    "    # == simplify notation == #\n",
    "    grid, γ = lt.grid, lt.γ\n",
    "    \n",
    "    i = 0\n",
    "    f = zeros(grid)  # Initial guess of f\n",
    "    error = tol + 1\n",
    "    \n",
    "    while (error > tol) && (i < max_iter)\n",
    "        f_new = lucas_operator(lt, f)\n",
    "        error = maximum(abs, f_new - f)\n",
    "        f = f_new\n",
    "        i += 1\n",
    "    end\n",
    "    \n",
    "    # p(y) = f(y) * y ^ γ\n",
    "    price = f .* grid.^γ\n",
    "    \n",
    "    return price\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "tree = LucasTree(γ=2.0, β=0.95, α=0.90, σ=0.1)\n",
    "price_vals = solve_lucas_model(tree);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using PyPlot\n",
    "\n",
    "plt[:plot](figsize=(12, 8))\n",
    "plt[:plot](tree.grid, price_vals, label=L\"$p*(y)$\")\n",
    "plt[:xlabel](L\"$y$\")\n",
    "plt[:ylabel](\"price\")\n",
    "plt[:legend]()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "for β in (.95, 0.98)\n",
    "    tree = LucasTree(;β=β)\n",
    "    grid = tree.grid\n",
    "    price_vals = solve_lucas_model(tree)\n",
    "    plt[:plot](grid, price_vals, label=latexstring(\"\\$ \\\\beta = $β\\$\"))\n",
    "end\n",
    "\n",
    "plt[:legend]()"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.6.2",
   "language": "julia",
   "name": "julia-0.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}